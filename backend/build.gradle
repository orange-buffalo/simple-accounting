buildscript {
    ext {
        kotlinVersion = '1.3.21'
        springBootVersion = '2.1.2.RELEASE'
        coroutinesVersion = '1.1.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'kotlin-kapt'

sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencyManagement {
    implementation {
        imports {
            mavenBom 'org.junit:junit-bom:5.2.0'
        }
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')
    compile('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    compile('org.jetbrains.kotlin:kotlin-reflect')
    compile('io.github.microutils:kotlin-logging:1.6.10')
    compile('io.jsonwebtoken:jjwt-api:0.10.5')
    compile('com.querydsl:querydsl-jpa:4.2.1')
    compile('com.github.kittinunf.result:result:1.5.0') //todo #68: use arrow
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-reactive:$coroutinesVersion")
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$coroutinesVersion")
    compile('org.springframework.security:spring-security-oauth2-client')

    kapt('com.querydsl:querydsl-apt:4.2.1:jpa')
    kapt('org.springframework.boot:spring-boot-configuration-processor')

    runtime('org.flywaydb:flyway-core')
    runtime('com.h2database:h2')
    runtime('io.jsonwebtoken:jjwt-impl:0.10.5')
    runtime('io.jsonwebtoken:jjwt-jackson:0.10.5')
    runtime('org.glassfish:javax.el:3.0.1-b10')

    compileOnly('org.projectlombok:lombok')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.projectreactor:reactor-test')
    testCompile('org.junit.jupiter:junit-jupiter-api')
    testCompile('org.junit.jupiter:junit-jupiter-params')
    testCompile('org.springframework.security:spring-security-test')
    testCompile('net.javacrumbs.json-unit:json-unit-assertj:2.0.2')
    testCompile('com.nhaarman:mockito-kotlin:1.6.0')
    testCompile('org.mockito:mockito-junit-jupiter:2.21.0')
    testCompile('org.flywaydb:flyway-core')
    testCompile('com.willowtreeapps.assertk:assertk-jvm:0.13')
    testRuntime('org.junit.jupiter:junit-jupiter-engine')
}

def frontendDistDir = "$buildDir/generated-resources/main"
sourceSets {
    main {
        output.dir(frontendDistDir, builtBy: 'copyFrontend')
    }
}

task copyFrontend(type: DefaultTask) {

    doLast {
        copy {
            from project(':frontend').file('dist')
            exclude 'admin/**'
            exclude 'app/**'
            into "$frontendDistDir/META-INF/resources"
        }

        copy {
            from project(':frontend').file('dist')
            include 'admin/**'
            include 'app/**'
            into "$frontendDistDir/META-INF/pages"
        }
    }

    dependsOn ':frontend:build'
}

test {
    useJUnitPlatform()

    beforeTest { descriptor ->
        logger.lifecycle("Running test: $descriptor")
    }
}